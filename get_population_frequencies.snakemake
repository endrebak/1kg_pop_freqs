from os import environ
environ['TMUX'] # will raise keyerror if not within tmux session

import pandas as pd

PANEL_FILE = "data/integrated_call_samples_v3.20130502.ALL.panel"
CHROMOSOME_FILES = "data/ALL.{chromosome}.phase3_shapeit2_mvncall_integrated_v5a.20130502.genotypes.vcf.gz"

rule all:
    input:
        expand("tmp/{chromosome}.csv", chromosome=["chr21", "chr22"])

rule create_bed_files:
    input:
        infile = CHROMOSOME_FILES
    output:
        "tmp/{chromosome}.bed",
        "tmp/{chromosome}.bim",
        "tmp/{chromosome}.fam"
    shell:
        "plink --vcf {input.infile} --snps-only --make-bed --maf 0.01 --out tmp/{wildcards.chromosome}"

rule add_pop_to_fam_files:
    input:
        infile = "tmp/{chromosome}.fam",
        panel = PANEL_FILE
    output:
        outfile = "tmp/{chromosome}_pop.fam"
    run:
        panel = pd.read_table(input.panel, sep="\s+", header=0, index_col=0)
        fam_file = pd.read_table(input.infile, sep="\s+", header=None, index_col=0)

        df = panel.join(fam_file)
        df = df.reset_index()

        df = df.drop(["sample", "super_pop", "gender"], axis=1)

        df.to_csv(output.outfile, header=False, index=False, sep=" ")

rule population_frequencies:
    input:
        fam = "tmp/{chromosome}_pop.fam",
        bed = "tmp/{chromosome}.bed",
        bim =  "tmp/{chromosome}.bim"
    output:
        outfile = "tmp/{chromosome}.frq.strat"
    run:
        outfile_prefix = output.outfile.split(".")[0]

        shell("plink --bed {input.bed} --bim {input.bim} --fam {input.fam} --freq --family --out {outfile_prefix}")

rule arrange_data:
    input:
        infile = "tmp/{chromosome}.frq.strat"
    output:
        outfile = "tmp/{chromosome}.csv"
    run:
        df = pd.read_table(input.infile, sep="\s+", header=0)
        df = df.drop(["MAC", "NCHROBS"] , axis=1) # + ["CHR", "SNP", "A1", "A2"]

        df = pd.pivot_table(df, index=["CHR", "SNP", "A1", "A2"], columns=["CLST"], values="MAF")#columns="CLST", values="MAF") #=)

        df.to_csv(output.outfile, sep=" ")

